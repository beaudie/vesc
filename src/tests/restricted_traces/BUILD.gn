# Copyright 2020 The ANGLE Project Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
#
# Contains the build rules for confidential trace tests.

import("../../../gni/angle.gni")

common_json_path = "restricted_traces.json"

angle_trace_library("angle_restricted_traces") {
  json_path = common_json_path
  trace_list = angle_restricted_traces
}

angle_trace_library("angle_restricted_traces_a_m") {
  json_path = common_json_path
  _trace_json = read_file(json_path, "json")
  trace_list = filter_include(_trace_json.traces,
                              [
                                "a*",
                                "b*",
                                "c*",
                                "d*",
                                "e*",
                                "f*",
                                "g*",
                                "h*",
                                "i*",
                                "j*",
                                "k*",
                                "l*",
                                "m*",
                              ])
}

angle_trace_library("angle_restricted_traces_n_z") {
  json_path = common_json_path
  _trace_json = read_file(json_path, "json")
  trace_list = filter_include(_trace_json.traces,
                              [
                                "n*",
                                "o*",
                                "p*",
                                "q*",
                                "r*",
                                "s*",
                                "t*",
                                "u*",
                                "v*",
                                "w*",
                                "x*",
                                "y*",
                                "z*",
                              ])
}

group("goldctl") {
  data = []

  # Because this links to a CIPD dependency, which is a symlink on Unix
  # platforms, refer to the actual executable rather than the whole directory;
  # copying the whole directory doesn't work, at least with "mb.py zip".
  if (is_win) {
    data += [ "//tools/skia_goldctl/win/goldctl.exe" ]
  } else if (is_mac) {
    data += [
      "//tools/skia_goldctl/mac_amd64/goldctl",
      "//tools/skia_goldctl/mac_arm64/goldctl",
    ]
  } else {
    data += [ "//tools/skia_goldctl/linux/goldctl" ]
  }
}

group("angle_restricted_trace_gold_tests") {
  testonly = true

  data_deps = [
    ":goldctl",
    "$angle_root/src/tests:angle_system_info_test",
  ]
  if (do_split_trace_test_apk) {
    data_deps += [
      "$angle_root/src/tests:angle_trace_tests_a_m",
      "$angle_root/src/tests:angle_trace_tests_n_z",
    ]
  } else {
    data_deps += [ "$angle_root/src/tests:angle_trace_tests" ]
  }
  data = [
    "restricted_trace_gold_tests.py",
    "restricted_traces.json",
    "../py_utils/android_helper.py",
    "../py_utils/angle_path_util.py",
    "../py_utils/angle_test_util.py",
    "../py_utils/skia_gold/",
    "//build/skia_gold_common/",
    "//testing/scripts/common.py",
    "//testing/xvfb.py",
  ]
}
