# Copyright 2020 The ANGLE Project Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# BUILD file for capture_replay_tests

import("../../../gni/angle.gni")
declare_args() {
  # Determines if we build the capture_replay_test. Off by default.
  angle_build_capture_replay_test = false

  # Decide which context to replay, starting with desktop default
  angle_capture_replay_test_context_id = 1
}

if (angle_build_capture_replay_test) {
  # TODO (nguyenmh): http://anglebug.com/4758: turn angle_executable into
  # angle_test when adding android support
  angle_executable("capture_replay_test") {
    testonly = true
    _contextid = angle_capture_replay_test_context_id
    sources = read_file("angle_capture_context${_contextid}_files.txt",
                        "list lines") +
              [
                "CaptureReplayTest.cpp",
                "angle_capture_context${_contextid}.cpp",
                "angle_capture_context${_contextid}.h",
              ]
    deps = [
      "$angle_root:angle_compression",
      "../../../:angle_common",
      "../../../util:angle_util",
      "../../../util:angle_util_loader_headers",
    ]
    configs += [ "../../../:library_name_config" ]

    suppressed_configs += [ "$angle_root:constructor_and_destructor_warnings" ]

    # Disable optimization to avoid optimizing huge files.
    if (!is_debug) {
      suppressed_configs += [ "//build/config/compiler:default_optimization" ]
      configs += [ "//build/config/compiler:no_optimize" ]
    }
    _data_path = rebase_path(".", root_out_dir)
    defines = [
      "ANGLE_CAPTURE_REPLAY_TEST_DATA_DIR=\"${_data_path}\"",
      "ANGLE_CAPTURE_REPLAY_TEST_CONTEXT_ID=${_contextid}",
      "ANGLE_CAPTURE_REPLAY_TEST_HEADER=angle_capture_context${_contextid}.h",
    ]
  }
}

group("capture_replay_tests") {
}
