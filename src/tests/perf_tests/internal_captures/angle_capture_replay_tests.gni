# Copyright 2020 The ANGLE Project Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../../../../gni/angle.gni")

_trex_frame_ranges = [
  "200_210",
  "800_810",
  "900_910",
  "1300_1310",
]

angle_capture_replay_sources = [ "perf_tests/CaptureReplayPerf.cpp" ]
angle_capture_replay_defines = []

# Internal TRex captures
foreach(_frame_range, _trex_frame_ranges) {
  _trex_dir = "trex_${_frame_range}"
  _trex_ctx = "${_trex_dir}/trex_${_frame_range}_capture_context1"

  # Similar to capture replay sample, use the file index for sources
  angle_capture_replay_sources +=
      rebase_path(read_file("${_trex_ctx}_files.txt", "list lines"),
                  ".",
                  "perf_tests/internal_captures/${_trex_dir}") +
      [
        "perf_tests/internal_captures/${_trex_ctx}.cpp",
        "perf_tests/internal_captures/${_trex_ctx}.h",
      ]

  if (is_android) {
    _trex_data_path = "/sdcard/chromium_tests_root/${angle_data_dir}/internal_captures/trex_${_frame_range}"
  } else {
    _trex_data_path = rebase_path("${_trex_dir}")
  }

  angle_capture_replay_defines +=
      [ "ANGLE_TREX_REPLAY_DATA_DIR_${_frame_range}=\"${_trex_data_path}\"" ]
}
