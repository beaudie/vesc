# Copyright 2019 The ANGLE Project Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
#
# This file houses the build configuration for the ANGLE Metal back-end.

import("../../../../gni/angle.gni")

assert(angle_enable_metal)

_metal_backend_sources = [
  "BufferMtl.h",
  "BufferMtl.mm",
  "CompilerMtl.h",
  "CompilerMtl.mm",
  "ContextMtl.h",
  "ContextMtl.mm",
  "DisplayMtl.h",
  "DisplayMtl.mm",
  "FrameBufferMtl.h",
  "FrameBufferMtl.mm",
  "GlslangWrapper.h",
  "GlslangWrapper.mm",
  "Metal_platform.h",
  "ProgramMtl.h",
  "ProgramMtl.mm",
  "RenderBufferMtl.h",
  "RenderBufferMtl.mm",
  "RenderTargetMtl.h",
  "RenderTargetMtl.mm",
  "RendererMtl.h",
  "RendererMtl.mm",
  "ShaderMtl.h",
  "ShaderMtl.mm",
  "StateCacheMtl.h",
  "StateCacheMtl.mm",
  "SurfaceMtl.h",
  "SurfaceMtl.mm",
  "TextureMtl.h",
  "TextureMtl.mm",
  "UtilsMtl.h",
  "UtilsMtl.mm",
  "VertexArrayMtl.h",
  "VertexArrayMtl.mm",
  "mtl_buffer_pool.h",
  "mtl_buffer_pool.mm",
  "mtl_command_buffer.h",
  "mtl_command_buffer.mm",
  "mtl_common.h",
  "mtl_common.mm",
  "mtl_format_table_autogen.mm",
  "mtl_format_utils.h",
  "mtl_format_utils.mm",
  "mtl_resources.h",
  "mtl_resources.mm",
  "mtl_utils.h",
  "mtl_utils.mm",
]

angle_source_set("angle_metal_backend") {
  sources = _metal_backend_sources

  cflags = []
  cflags_cc = []
  cflags_objc = []
  cflags_objcc = []
  ldflags = []
  libs = []

  public_deps = [
    "${angle_root}:libANGLE_headers",
  ]

  deps = [
    "${angle_glslang_dir}:glslang_default_resource_limits_sources",
    "${angle_glslang_dir}:glslang_sources",
    "${angle_spirv_cross_dir}:spirv_cross_sources",
  ]

  # Due to autoreleasepool unpredictable problems, we disable ARC for now.
  cflags_objc += [
    "-fno-objc-arc",
    "-Wno-nullability-completeness",
  ]
  cflags_objcc += [
    "-fno-objc-arc",
    "-Wno-nullability-completeness",
  ]

  libs += [ "Metal.framework" ]

  if (is_mac) {
    additional_cflags = [
      # This should be set by setting "mac_deployment_target" argument
      # "-mmacosx-version-min=10.13"
    ]

    cflags += additional_cflags
    cflags_cc += additional_cflags
    cflags_objc += additional_cflags
    cflags_objcc += additional_cflags

    ldflags += additional_cflags

    libs += [
      "Cocoa.framework",
      "IOSurface.framework",
      "QuartzCore.framework",
    ]
  }

  # TODO(hqle): iOS support.
}
